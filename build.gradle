buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-clover-plugin:3.0.1'
    }
}

plugins {
    id 'eclipse'
    id 'idea'
    id 'groovy'
    id 'application'
}

apply plugin: 'com.bmuschko.clover'

mainClassName = 'com.seriouslypro.pnpconvert.PNPConvert'

version = "2.0.0"

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/service/local/content/repositories/staging/"
    }
}

task createProperties(dependsOn: processResources) {
    doLast {
        def resourcesDirectory = "src/main/resources".toString()
        new File(resourcesDirectory).mkdirs()
        new File("$resourcesDirectory/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:3.0.8'
    implementation 'commons-codec:commons-codec:1.10'
    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
    testImplementation 'org.spockframework:spock-junit4:2.0-groovy-3.0'
    testImplementation group: 'cglib', name: 'cglib-nodep', version: '2.2'
    testImplementation group: 'org.objenesis', name: 'objenesis', version: '3.0.1'
    testImplementation 'org.springframework.boot:spring-boot:1.2.1.RELEASE' // For OutputCapture

    implementation 'info.picocli:picocli:4.6.1'

    implementation 'net.sf.opencsv:opencsv:2.3'

    implementation 'commons-cli:commons-cli:1.3.1'

    implementation 'net.glxn.qrgen:javase:2.0'
    implementation 'org.apache.pdfbox:pdfbox:2.0.0-RC3'

    implementation group: 'org.apache.xmlgraphics', name: 'batik-svggen', version: '1.17'
    implementation group: 'org.apache.xmlgraphics', name: 'batik-dom', version: '1.17'

    implementation 'com.google.api-client:google-api-client:2.2.0'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.34.1'
    implementation 'com.google.apis:google-api-services-sheets:v4-rev20230227-2.0.0'

    clover 'org.openclover:clover:4.4.1'
}

test {
    useJUnitPlatform()

    afterSuite { desc, result ->
        if (!desc.parent) {
            if (result.testCount == 0) {
                throw new IllegalStateException("No tests were found. Failing the build")
            }
        }
    }
}

clover {
    // https://github.com/bmuschko/gradle-clover-plugin
    testIncludes = ['**/*Spec.groovy']
    excludes = [
        // All the following cause: java.lang.UnsupportedOperationException: EmptyExpression.INSTANCE is immutable"
        '**/Feeders.groovy',
        '**/FeederTester.groovy',
        '**/BoardMirroring.groovy',
        '**/DiptraceComponentPlacementTransformer.groovy',
        '**/DipTracePlacementsLineParser.groovy',
        '**/DPVFile.groovy',
        '**/DPVGenerator.groovy',
        '**/RowMatcher.groovy',
    ]
    testExcludes = [
            // All the following cause: java.lang.UnsupportedOperationException: EmptyExpression.INSTANCE is immutable"
            '**/DPVFileParserSpec.groovy',
    ]

    report {
        html = true
        xml = true
    }
}